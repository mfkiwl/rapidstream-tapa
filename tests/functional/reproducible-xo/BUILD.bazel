"""Tests that xo files generated by TAPA is reproducible."""

# Copyright (c) 2024 RapidStream Design Automation, Inc. and contributors.
# All rights reserved. The contributor(s) of this file has/have agreed to the
# RapidStream Contributor License Agreement.

load("@rules_shell//shell:sh_test.bzl", "sh_test")
load("//bazel:tapa_rules.bzl", "tapa_xo")

tapa_xo(
    name = "vadd-xo",
    src = "//tests/apps/vadd:vadd.cpp",
    hdrs = ["//tests/apps/vadd:vadd.h"],
    include = ["//tests/apps/vadd:."],
    top_name = "VecAdd",
)

sh_test(
    name = "reproducible-xo-test",
    srcs = ["xo-diff.sh"],
    args = [
        "$(location :vadd-xo)",
        "$(location //tests/apps/vadd:vadd-xo)",
    ],
    data = [
        ":vadd-xo",
        "//tests/apps/vadd:vadd-xo",
    ],
)

genrule(
    name = "reuse-work-dir-vadd-xo",
    srcs = [
        "//tests/apps/vadd:vadd.cpp",
        "//tests/apps/vadd:vadd.h",
    ],
    outs = ["reuse-work-dir-vadd.xo"],
    cmd_bash = """
set -ex
work_dir="work.out"
part_name="xcvu13p-flga2577-2L-e"
clock_period="3.33"

$(location //bazel:vitis_hls_env) $(location //tapa) --work-dir "$${work_dir}" \
    analyze -c -I$$(dirname $(location //tests/apps/vadd:vadd.h)) \
    --input $(location //tests/apps/vadd:vadd.cpp) --top VecAdd \

$(location //bazel:vitis_hls_env) $(location //tapa) --work-dir "$${work_dir}" \
    synth --part-num "$${part_name}" --clock-period "$${clock_period}" \
    --override-report-schema-version=redacted \

$(location //bazel:vitis_hls_env) $(location //tapa) --work-dir "$${work_dir}" \
    synth --part-num "$${part_name}" --clock-period "$${clock_period}" \
    --skip-hls-based-on-mtime \
    --override-report-schema-version=redacted \

$(location //bazel:vitis_hls_env) $(location //tapa) --work-dir "$${work_dir}" \
    pack --output $@ \

""",
    tools = [
        "//bazel:vitis_hls_env",
        "//tapa",
    ],
)

# XO generated step-by-step should be the same as XO generated in one step.
sh_test(
    name = "reuse-work-dir-test",
    srcs = ["xo-diff.sh"],
    args = [
        "$(location :reuse-work-dir-vadd-xo)",
        "$(location //tests/apps/vadd:vadd-xo)",
    ],
    data = [
        ":reuse-work-dir-vadd-xo",
        "//tests/apps/vadd:vadd-xo",
    ],
)
